#!/usr/bin/env perl

use strict;
use warnings;
use 5.024;
use feature      ('refaliasing');
use experimental ('refaliasing');

use Image::Info qw(image_info dim);
use Path::Class;
use File::Copy;

my $dir = dir( $ARGV[0] ) || die "no directory specified to $0";

my %allsizes = (
    landscape => [
        [ imac_27_retina => 5120, 2880, ],
        [ macbook_pro_16 => 3072, 1920, ],
        [ macbook_pro_13 => 2560, 1600, ],
        [ imac_27        => 2560, 1440, ],
        [ macbook_12     => 2304, 1440, ],
        [ fhd            => 1920, 1080, ],
    ],
    portrait => [
        [ monitor_30_p  => 1600, 2560, ],
        [ iphone_xs_max => 1242, 2668, ],
        [ ipad_4        => 1536, 2048, ],
        [ iphone_xr     => 828,  1792, ],
    ]

);

my $getsize_cr = sub {
    my $file = shift;
    return if $file->is_dir;
    my ($info) = image_info("$file");
    if ( $info->{error} ) {
        say "$file: error";
        return;
    }
    my ( $i_wid, $i_ht ) = dim($info);
    #say "$file:\n    ${i_wid}x$i_ht";

    \my @sizes
      = ( $i_wid < $i_ht ) ? $allsizes{portrait} : $allsizes{landscape};

    my $chosen;
  SIZE:
    for \my @size(@sizes) {
        my ( $s_name, $s_wid, $s_ht ) = @size;
        if ( $i_wid >= $s_wid and $i_ht >= $s_ht ) {
            $chosen = $s_name;
            last SIZE;
        }
    }

    $chosen //= 'small';
    $chosen = "wallpaper-$chosen";

    #say "$file:\n    ${i_wid}x$i_ht   $chosen";
    my $basename = $file->basename;
    mkdir $chosen unless -d $chosen;
    unless ( -e "$chosen/$basename" ) {
        move "$file", "$chosen/$basename";
    }

};

$dir->recurse( callback => $getsize_cr );

__END__

